/* ramresident-gnu.ldscript
 *
 * Linker script for a program that is resident in RAM (Gnu linker)
 * Documentation: http://www.gnu.org/software/binutils/manual/ld-2.9.1/html_node/ld_17.html
 */
SECTIONS
{
    . = 0xa1000000;
    OSEKOS_interruptVectors    : { *(OSEKOS_interruptVectors)    }
    OSEKOS_exceptionVectors    : { *(OSEKOS_exceptionVectors)    }
    .text                      : { *(.text)        }
    .rodata                    : { *(.rodata)        }
    .zdata    BLOCK(8)         : { *(.zdata)        }
    .sdata                     : { *(.sdata)        }
    .data                      : { *(.data)        }
    __STARTBSS = . ;
    .zbss                      : { *(.zbss)        }
    .sbss                      : { *(.sbss)        }
    .bss                       : { *(.bss)        }
    __ENDBSS = . ;
        keso_heap_start = . ;
    .objheap                   : { *(.objheap)     }
        keso_heap_end = . ;
        keso_stack_start = . ;
    .osek_stacks               : { *(.osek_stacks)    }
        keso_stack_end = . ;

  /* These sections appear to be generated by the compiler.
   * We don't use them.
  */
    .stack                     : { *(.stack)        }

  /* These sections are DWARF-2 debug sections. They contain
   * ELF relocations and must be located at zero.
  */
    . = 0x0;
    .debug_aranges             : { *(.debug_aranges)    }
    . = 0x0;
    .debug_pubnames            : { *(.debug_pubnames)    }
    . = 0x0;
    .debug_info                : { *(.debug_info)    }
    . = 0x0;
    .debug_abbrev              : { *(.debug_abbrev)    }
    . = 0x0;
    .debug_line                : { *(.debug_line)    }

   /* Scratchpad RAM of the Tricore CPU, divided in
      Program Scratchpad RAM (PSPR)  0xd7ffffff - 0xd4000000
      and Data Scratchpad RAM (DSPR) 0xd3ffffff - 0xd0000000
    */
   TRICORE_PSPR 0xd4000000 : {
       *(.tricore_pspr)
   }
   TRICORE_DSPR 0xd0000000 : {
       *(.tricore_dspr)
   }
}
